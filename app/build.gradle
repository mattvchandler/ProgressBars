apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.ver_major = 1
ext.ver_minor = 6
ext.ver_patch = 5
ext.min_sdk   = 19

private Integer gen_version_code()
{
    // version code is <MIN_SDK>0<2-digit MAJOR><2-digit MINOR><2-digit PATCH>
    return ext.min_sdk   * 10000000 +
           ext.ver_major *    10000 +
           ext.ver_minor *      100 +
           ext.ver_patch
}

private String gen_version_name()
{
    return "${ext.ver_major}.${ext.ver_minor}.${ext.ver_patch}"
}

android {
    compileSdkVersion 28
    dataBinding.enabled = true
    defaultConfig {
        applicationId "org.mattvchandler.progressbars"
        minSdkVersion project.ext.min_sdk
        targetSdkVersion 28
        versionCode gen_version_code()
        versionName gen_version_name()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.1.0-alpha04'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha03'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
